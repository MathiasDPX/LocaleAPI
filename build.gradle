plugins {
    id 'java'
    id 'maven-publish'
}

group = 'gg.gyro'
version = '1.2.0'

repositories {
    mavenCentral()
    maven { url "https://jitpack.io" }
    maven { url 'https://repo.papermc.io/repository/maven-public/' }

    maven {
        name = "spigotmc-repo"
        url = "https://hub.spigotmc.org/nexus/content/repositories/snapshots/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
}

dependencies {
    compileOnly("org.spigotmc:spigot-api:1.18.2-R0.1-SNAPSHOT")

    testImplementation 'org.slf4j:slf4j-simple:2.0.16'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.11.0'
    testImplementation 'com.github.seeseemelk:MockBukkit-v1.18:2.85.2'
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'

    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release.set(targetJavaVersion)
    }
}

jar {
    destinationDirectory.set(file("./builds/"))
}

tasks.register('javadocJar', Jar) {
    destinationDirectory.set(file("./builds/"))
    group = 'documentation'
    dependsOn javadoc
    from(file("${buildDir}/docs/javadoc")) {
        into('/')
    }
    archiveClassifier.set('javadoc')
    description = 'Creates a Javadoc Jar'
}

tasks.register('sourcesJar', Jar) {

    destinationDirectory.set(file("./builds/"))
    archiveClassifier.set('sources')
    group = "documentation"
    from sourceSets.main.allSource
}

clean.doFirst {
    delete "${rootDir}/builds"
}

tasks.register('final-jar') {
    dependsOn "clean", "jar", "javadocJar", "sourcesJar"

    group = 'build setup'
    description = 'Builds the plugin jar, javadoc jar, and sources jar'
}

publishing {
    repositories {
        maven {
            url = uri('https://mvn.coolcraft.ovh/releases')
            credentials {
                username = System.getenv("MAVEN_USERNAME") ?: project.findProperty("MAVEN_USERNAME")
                password = System.getenv("MAVEN_PASSWORD") ?: project.findProperty("MAVEN_PASSWORD")
            }
        }
    }

    publications {
        mavenJar(MavenPublication) {
            from components.java
            artifact(javadocJar) {
                classifier "javadoc"
            }

            artifact(sourcesJar) {
                classifier "sources"
            }

            artifactId = 'LocaleAPI'
            groupId = 'gg.gyro'
            version = 'snapshot'
        }
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

test {
    useJUnitPlatform()
}
